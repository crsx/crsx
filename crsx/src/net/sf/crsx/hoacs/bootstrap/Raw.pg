/**
 * Grammar for "raw" HOACS systems, in PG form.
 * Used to bootstrap HOACS on a crsx3 system.
 *
 * @author <a href="http://www.research.ibm.com/people/k/krisrose">Kristoffer Rose</a>.
 * @version $Id: Raw.pg,v 1.32 2013/12/05 03:28:29 krisrose Exp $
 */

class net.sf.crsx.hoacs.bootstrap.Raw : <Module>, <ModuleName>, <EmbeddedModule>,
<Declaration>, <Declarations>, <SortAbstraction>, <Rule>, <Sort>, <SortParam>, <SortName>, <SortNames>, <SimpleSort>, <SimpleSorts>, <SubstituteSorts>, <SubstituteSort>, <Tagging>,
<SortAlternative>, <SortAlternatives>, <Form>, <FormPrec>, <InheritedRefs>, <InheritedRef>, <AttributeKind>, <AttributeForm>, <AttributeOptions>, <AttributeOption>, <LexerState>,
<ScopeSorts>, <ScopeSort>, <ParsedForm>, <Repeat>, <Priority>,
<FragmentName>, <FragmentRef>, <RegExp>, <RegExpChoice>, <RegExpUnits>, <RegExpUnit>, <RegExpSimple>, <RegExpLeaf>, <Word>,
<Term>, <NotVariableTerm>, <Attributes>, <Attribute>, <AttributeValue>, <Scopes>, <Scope>, <Terms>, <Parsed>,
<Literal>, <Constructor>, <MetaVariable>, <MetaVariables>, <Variable>, <Variables>, <AttributeName>,
<RegExpClass>, <CC>, <CC_RangesTop>, <CC_Ranges>, <CC_FirstRangeStartChar>, <CC_NonDashRangeStartChar>, <CC_RangeChar>

prefix raw

declarations
%{
  static String raw(String s) {return "RAW-"+s;} // prefix for constructors
  static String unembed(String s) {return s.substring(1, s.length()-1);}
%}

//// HOACS META SYNTAX.

<Embedded> (*) ::= Embedded$$:unembed .
token Embedded (*) ::= ( '«' (~['«','»'])* '»' ).

meta[<SortParam>]  ::=  '#<PRODUCTION_NAME>' ( (DIGIT)* | '_' (DIGIT | UPPER | LOWER)* ) '#' , '⦃', '⦄', '∥' .
inject[<Embedded>]  ::=  '%<PRODUCTION_NAME>', '', '' .

//// STRUCTURE.

<Module> ::= {top-module}:raw <EmbeddedModule> .

<EmbeddedModule> ::= MODULE:raw <ModuleName> LBRACE <Declarations> RBRACE .

<Declarations> ::= ( {Declarations$Cons}:raw <Declaration> <Declarations> | {Declarations$Nil} ).

<Declaration> ::=
  {embedded-module}:raw <EmbeddedModule> SEMI
| IMPORT:raw <ModuleName> ( LPAREN (<SortNames> | {$Nil}) RPAREN | {$Nil} ) SEMI

| SPACE:raw <RegExp> SEMI
| TOKEN ( {'token'}:raw <SortName> BAR <RegExp> <LexerState> SEMI | FRAGMENT:raw <FragmentName> BAR <RegExp> SEMI )
| FRAGMENT:raw <FragmentName> BAR <RegExp> SEMI

| ATTRIBUTE:raw <AttributeKind> <AttributeName> <AttributeForm> <AttributeOptions> SEMI

| SORT:raw <SortName> <Repeat> <SortAbstraction> SEMI
| {anonymous}:raw <SortAlternatives> SEMI
| (RULE)? {rule}:raw <Rule> SEMI
| {nestedDeclarations}:raw LBRACE <Declarations> RBRACE
| LEXER:raw <SortName> SEMI
.

//// SYNTAX.

<LexerState> (RE,DEFAULT) ::= {ChangeState}:raw ARROW <SortName> | {LexerState$Nil}.

<RegExp> (RE) ::= {RegExp$Cons} <RegExpChoice> ( BAR <RegExp> | {RegExp$Nil} ) .

<RegExpChoice> (RE) ::=
  NESTED:raw <RegExpSimple> <RegExpSimple>
| {RegExpConcat}:raw <RegExpUnits>
.

<RegExpUnits> (RE) ::= {$Cons} <RegExpUnit> <RegExpUnits> | {$Nil} .

<RegExpUnit> (RE) ::= {RegExpUnit}:raw <RegExpSimple> <Repeat> .

<Repeat> (DEFAULT,RE) ::=
  PLUS ( {RepeatSomeSep}:raw SUB (<RegExpLeaf> | {RegExpWord}:raw <Separator>) | {RepeatSome}:raw )
| {RepeatMaybeSome}:raw STAR
| {RepeatMaybe}:raw QUEST
| {RepeatSingle}:raw
.

<RegExpLeaf> (DEFAULT,RE) ::=
  {RegExpString}:raw <String>
| {RegExpWord}:raw <Word>
.

<RegExpSimple> (RE) ::=
  <RegExpLeaf>
| {RegExpRef}:raw <FragmentRef>
| {RegExpClass}:raw <RegExpClass>
| {RegExpAny}:raw DOT
| {RegExpClass}:raw {RegExpClassChar}:raw (ESCAPEDCHAR$) {RegExpClassDone}:raw
| {RegExpNest}:raw LPAREN <RegExp> RPAREN
.


//// SORTS.

<AttributeForm> ::=
  {AttributeFormSimple}:raw LPAREN <Sort> RPAREN
| LBRACE <Sort>:# ( {AttributeFormMap}:raw {{#}} COLON <Sort> RBRACE
                  | {AttributeFormSet}:raw {{#}} RBRACE )
.

<AttributeOptions> ::= {$Cons} <AttributeOption> <AttributeOptions> | {$Nil} .
<AttributeOption> ::= {AttributeOption}:raw <Variable> <Constructor> .

<SortNames> ::= {$Cons} <SortName> (COMMA <SortNames> | {$Nil}) . 

<SortAbstraction> ::=
  {SortAbstractionParam}:raw <Variable>^a <SortAbstraction>[a]
| {SortAbstractionBody}:raw <SortAlternatives>
.

<SortAlternatives> ::= {$Cons} <SortAlternative> <SortAlternatives> | {$Nil} .

<SortAlternative> ::= BAR (
  SCHEME:raw <Form>
| {SynthesizedRef}:raw UP <AttributeName> 
| SUGAR:raw <Form> ARROW <Term>
| SYMBOL:raw <Form>
| STATIC:raw (SYMBOL)? <Form>
| {data}:raw (DATA)? <Form> <Tagging>
).

<Form> ::=
  <Constructor>:#C
    ( {FormParsedSorted}:raw {{#C}} CONCRETESTART <ParsedForm> CONCRETEEND <FormPrec> <InheritedRefs>
    | {FormConstruction}:raw {{#C}} ( LPAREN (<ScopeSorts> | {$Nil}) RPAREN | {$Nil} ) <InheritedRefs>
    )
| {FormParsedUnsorted}:raw CONCRETESTART <ParsedForm> CONCRETEEND <FormPrec> <InheritedRefs>
.
<ScopeSorts> ::= {$Cons} <ScopeSort> (COMMA <ScopeSorts> | {$Nil}) . 
<ScopeSort> ::=
  {ScopeSort}:raw <Sort> <Repeat> (LBRACKET (<SubstituteSorts> | {$Nil}) RBRACKET | {$Nil})
| {ScopeSortBinder}:raw LBRACKET <Variable> COLON <Sort> RBRACKET
.

<SubstituteSorts> ::= {$Cons} <SubstituteSort> (COMMA <SubstituteSorts> | {$Nil}) . 
<SubstituteSort> ::= {SubstituteSort}:raw <Variable> COLON <Sort> .

<Sort> ::= {Sort}:raw <SimpleSort> <SimpleSorts> .

<SimpleSorts> ::= {$Cons} <SimpleSort> <SimpleSorts> | {$Nil} .

<SimpleSort> ::=
  {SortName}:raw <SortName> <MetaVariables>
| {SortParam}:raw <SortParam>
| LPAREN <Sort> RPAREN
.
<MetaVariables> ::= {$Cons} <MetaVariable> {$Nil} | {$Nil} .
<SortParam> ::= <Variable>! .

<ParsedForm> ::=
  {ParsedFormSpace}:raw <ConcreteSpace> <ParsedForm>
| {ParsedFormWord}:raw <ConcreteWord> <ParsedForm>
| CONCRETEPUSH {ParsedFormTerm}:raw <ScopeSort> <FormPrec> <Repeat> CONCRETEPOP <ParsedForm>
| {ParsedFormDone}:raw
.

<FormPrec> ::= ( AT <Natural> | {"0"} ) .

<Tagging> ::= ( {$Cons} TAG <Constructor> ({$Nil}) | {$Nil} ).

<InheritedRefs> ::=( {$Cons} <InheritedRef> <InheritedRefs> | {$Nil} ).
<InheritedRef> ::= {InheritedRef}:raw DOWN <AttributeName> . 


//// RULES.

<Rule> ::=
 {Rule}:raw <Priority>
 <Construction>:#first
 ( COLON {{#first}} <Construction> | {{OMITTED}} {{#first}} )
 ( ARROW <Term> | {{OMITTED}} )
.
<Priority> ::=( DEFAULT:raw | PRIORITY:raw | {Normal}:raw ).

//// TERMS.

<Term> ::=
  <Variable>:#v ( {Special}:raw {{#v}} <Term> | {VariableUse}:raw {{#v}} <Attributes>)
| <NotVariableTerm>
.

<NotVariableTerm> ::=
  <Constructor>:#C
  <Repeat>:#R
  ( {ParsedSorted}:raw {{#C}} {{#R}} CONCRETESTART <Parsed> CONCRETEEND <Attributes>
  | {MetaApplicationSorted}:raw {{#C}} {{#R}} <MetaVariable> ( LBRACKET (<Terms> | {$Nil}) RBRACKET | {$Nil} ) <Attributes>
  | {ConstructionSorted}:raw {{#C}} {{#R}} <Constructor> ( LPAREN (<Scopes> | {$Nil}) RPAREN
                                                         | LBRACKET (<Scopes> | {$Nil}) RBRACKET
                                                         | {$Nil}                                ) <Attributes>
  | {VariableUseSorted}:raw {{#C}} {{#R}} <Variable> <Attributes>
  | {Construction}:raw {{#C}} ( LPAREN (<Scopes> | {$Nil}) RPAREN
                              | LBRACKET (<Scopes> | {$Nil}) RBRACKET
                              | {$Nil}                                ) <Attributes>
  )
| {Literal}:raw <Literal> <Attributes>
| {Parsed}:raw CONCRETESTART <Parsed> CONCRETEEND <Attributes>
| {MetaApplication}:raw <MetaVariable> ( LBRACKET (<Terms> | {$Nil}) RBRACKET | {$Nil} ) <Attributes>
.

// Note: these are subsets of the above, generating identical terms.
<Construction> ::=
  <Constructor>:#C
  <Repeat>:#R
  ( {ParsedSorted}:raw {{#C}} {{#R}} CONCRETESTART <Parsed> CONCRETEEND <Attributes>
  | {ConstructionSorted}:raw {{#C}} {{#R}} <Constructor> ( LPAREN (<Scopes> | {$Nil}) RPAREN
                                                         | LBRACKET (<Scopes> | {$Nil}) RBRACKET
                                                         | {$Nil}                                ) <Attributes>
  | {Construction}:raw {{#C}} ( LPAREN (<Scopes> | {$Nil}) RPAREN
                              | LBRACKET (<Scopes> | {$Nil}) RBRACKET
                              | {$Nil}                                ) <Attributes>
  )
| {Literal}:raw <Literal> <Attributes>
| {Parsed}:raw CONCRETESTART <Parsed> CONCRETEEND <Attributes>
.

<Scopes> ::= {$Cons} <Scope> (COMMA <Scopes> | {$Nil}) .

<Scope> ::=
  {Scope}:raw ({$Nil}) <NotVariableTerm>
| <Variable>:#v ({Scope}:raw ({$Cons} {{#v}} <Variables>) DOT <Term> | {Scope}:raw ({$Nil}) ({VariableUse}:raw {{#v}}))
| {Scope}:raw LBRACKET (<VariableList> | {$Nil}) RBRACKET <Term>
.

<Variables> ::= {$Cons} <Variable> <Variables> | {$Nil} .
<VariableList> ::= {$Cons} <Variable> ( COMMA <VariableList> | {$Nil} ) .

<Terms> ::= {$Cons} <Term> (COMMA <Terms> | {$Nil}) .

<Parsed> ::=
  CONCRETESPACE <Parsed>
| {ParsedWord}:raw <ConcreteWord> <Parsed>
| CONCRETEPUSH {ParsedTerm}:raw <Term> CONCRETEPOP <Parsed>
| {ParsedDone}:raw
.

<Attributes> ::= ( {$Cons} <Attribute> <Attributes> | {$Nil} ).
<Attribute> ::=
  {Attribute}:raw <AttributeKind> <AttributeName> <AttributeValue>
.

<AttributeKind> ::= ( {AttributeKindUp}:raw UP | {AttributeKindDown}:raw DOWN ).

<AttributeValue> ::= 
  {AttributeValue}:raw LPAREN <Term> RPAREN
| LBRACE ( <Term>:# ( {AttributeKeyValue}:raw {{#}} COLON <Term> RBRACE
                    | {AttributeKey}:raw {{#}} RBRACE )
         | {AttributeNotKey}:raw NOT <Term> RBRACE )
.

//// TOKENS.

// Special syntactic tokens for concrete syntax.

token CONCRETESTART (DEFAULT : CONCRETE) ::= "\u27e6" . // ⟦
token CONCRETEEND (CONCRETE : DEFAULT) ::= "\u27e7" . // ⟧
token CONCRETEPUSH (CONCRETE : DEFAULT) ::= "\u27e8" . // ⟨
token CONCRETEPOP (DEFAULT : CONCRETE) ::= "\u27e9" . // ⟩

<ConcreteSpace> ::= CONCRETESPACE$ .
<ConcreteWord> ::= CONCRETEWORD$ .

token CONCRETESPACE (CONCRETE) ::= [' ',\t\r\n,\ub6]+ . // no comments but include special spacing marks
token CONCRETEWORD (CONCRETE) ::= ~[' ',\t\r\n\ub6\u27e6\u27e7\u27e8\u27e9]+ .

// Tokens for default (raw) terms.

skip (DEFAULT,RE) ::= ' ' | \r | \t | \n | '//' ~[\n\r]* .
%{
// Nested comments JavaCC hack...
TOKEN_MGR_DECLS : { int commentNesting ; int commentState; }
<DEFAULT,RE> SKIP : { "/*" { commentNesting = 1; commentState = curLexState; } : NESTED_COMMENT }
<NESTED_COMMENT> SKIP : { "/*" { commentNesting += 1; } }
<NESTED_COMMENT> SKIP : { "*/" { commentNesting -= 1; if (commentNesting<=0) SwitchTo(commentState); } }
<NESTED_COMMENT> SKIP : { < ~[] > }
%}

// Combinations.
<ModuleName> ::= <String> | <Variable> | <Constructor> . //TODO: allow dots inline.
<SortName> (DEFAULT,RE) ::= <Constructor> .
<FragmentName> (RE) ::= <Constructor> .
<FragmentRef> (DEFAULT,RE) ::= FRAGMENT_REF$:unembed .
<AttributeName> ::= <Literal> | <Variable> | <Constructor> .
<Constructor> ::= ( UPPER$ | DASH$ | CON$ ).
<Variable> ::= ( LOWER$ | VAR$ | MODULE$ | SORT$ | DATA$ | STATIC$ | SYMBOL$ | SCHEME$ | AUTOMATIC$ | ATTRIBUTE$ | COMPOSE$ | SUGAR$ | RULE$ | PRIORITY$ | DEFAULT$ | SPACE$ | IMPORT$ | TOKEN$ | LEXER$ | FRAGMENT$ | NESTED$ | TAG$ ).
<MetaVariable> ::= ( META$ | HASH$ ).
<Separator> ::= ( BAR$ | SEMI$ | COMMA$ | COLON$ | STAR$ | DOT$ ).

// Single character tokens.

// Common to regular expressions.
token SEMI (DEFAULT,RE : DEFAULT) ::= ";" .
token BAR (DEFAULT,RE) ::= "|" .
token LPAREN (DEFAULT,RE) ::= "(" .
token RPAREN (DEFAULT,RE) ::= ")" .
token STAR (DEFAULT,RE) ::= "*" .
token PLUS (DEFAULT,RE) ::= "+" .
token QUEST (DEFAULT,RE) ::= "?" .
token SUB (DEFAULT,RE) ::= "_" .
token DOT (DEFAULT,RE) ::= "." .
token COLON (DEFAULT,RE) ::= ":" .

// Exclusive to default mode.
token LBRACE ::= "{" .
token RBRACE ::= "}" .
token HASH ::= "#" .
token LBRACKET ::= "[" .
token RBRACKET ::= "]" .
token COMMA ::= "," .
token FORALL ::= \u2200 . // ∀
token ARROW (DEFAULT,RE : DEFAULT) ::= \u2192 . // →
token UP ::= \u2191 . // ↑
token DOWN ::= \u2193 . // ↓
token NOT ::= \u00ac . // ¬
token AT ::= "@" .
token DASH ::= "-" .

// Categories are separate tokens so remember to include where needed!
token DIGIT ::= [0-9] .
token LOWER ::= [a-z] .
token UPPER (DEFAULT,RE) ::= [A-Z] .

// Normal keywords (all special cases of VAR).

token MODULE ::= "module" .
token SORT ::= "sort" .
token DATA ::= "data" .
token SCHEME ::= "scheme" .
token AUTOMATIC ::= "automatic" .
token SYMBOL ::= "symbol" .
token STATIC ::= "static" .
token ATTRIBUTE ::= "attribute" .
token COMPOSE ::= "compose" .
token SUGAR ::= "sugar" .
token RULE ::= "rule" .
token PRIORITY ::= "priority" .
token DEFAULT ::= "default" .
token IMPORT ::= "import" .
token TAG ::= "tag" .
token LEXER ::= "lexer" .

// Regular Expression introduction keywords.
token SPACE (DEFAULT : RE) ::= "space" .
token TOKEN (DEFAULT : RE) ::= "token" .
token FRAGMENT (DEFAULT,RE : RE) ::= "fragment" .
token NESTED (RE) ::= "nested" .

// The basic identifier forms.
token META ::= HASH (NAME | STRING)? (HASH NAME)* SUFFIX* .
token VAR ::= LOWER NAME? SUFFIX* | "v" STRING SUFFIX* .
token CON (DEFAULT,RE) ::= (UPPER | [$]) NAME? SUFFIX* | "C" STRING SUFFIX* .
token fragment SUFFIX ::= "_" NAME? | "_"? NATURAL .
token fragment NAME ::= (LOWER | UPPER | [$])+ .

token FRAGMENT_REF (RE) ::= \u27e8 (UPPER | DASH | CON) \u27e9 . //Note: fits with <FragmentName>

// Constant values.
<Literal> ::= <String> | <Number> .

// Numbers.
<Number> ::= <Natural> | UNNATURAL$ .
<Natural> ::= DIGIT$ | NATURAL$ .
token NATURAL ::= DIGIT+ .
token fragment INTEGER ::= [-+]* NATURAL .
token UNNATURAL ::= (INTEGER? "." NATURAL) ( [Ee] INTEGER )? | INTEGER [Ee] INTEGER .

// COMMON TOKENS.

// Strings (in raw uninterpreted form).
<String> (DEFAULT,RE) ::= STRING$ .
token STRING (DEFAULT,RE) ::= "'" STRING1CHAR* "'" | '"' STRING2CHAR* '"' .
token fragment STRING1CHAR ::= ~[\\'\n] | ESCAPEDCHAR .
token fragment STRING2CHAR ::= ~[\\"\n] | ESCAPEDCHAR .

// Escaped characters...TODO: many more to add!
token ESCAPEDCHAR (DEFAULT,RE) ::=
  [\\] ~[0-9uUx]
| [\\] [0-3]? [0-7]? [0-7]
| [\\] [u] HEX? HEX? HEX? HEX
| [\\] [U] HEX? HEX? HEX? HEX? HEX? HEX? HEX? HEX
| [\\] [x] (HEX HEX | '{' HEX+ '}')
.
token fragment HEX ::= [0-9A-Fa-f] .

// Catch-all...
token OTHERCHAR ::= ~[] .

<Word> (DEFAULT,RE) ::= (UPPER$ | DASH$ | CON$ | WORD$) .
token WORD (RE) ::= ~['⟨','⟩','⟦','⟧','(',')','[',']','.','*','+','?',';','"',"'",',',':','«','»',' ',\t,\\,\n]+ .


// SUBPARSER FOR REGULAR EXPRESSION CHARACTER CLASSES.

// Tokenizer for *entire* character class expression.
<RegExpClass> (RE) ::= CHAR_CLASS$$:unembed:{rawCC} .
token CHAR_CLASS (RE) ::= '[' ('^')? (']')? (~[']',\\,\n] | ESCAPEDCHAR)* ']' .

// (Re)Parser to parse the actual regular expression.
<CC> (CC) ::= <CC_RangesTop> .

// All tokens are in CC mode.
token CC_NOT (CC) ::= "^" .
token CC_RBRACKET (CC) ::= "]" .
token CC_DASH (CC) ::= "-" .
token CC_OTHER (CC) ::= ~['^',']','-',\\,\n] | ESCAPEDCHAR .

<CC_RangesTop> (CC) ::=
  {RegExpClassNot}:raw CC_NOT <CC_FirstRangeStartChar>:#lo <CC_RangeTail #lo>
| <CC_FirstRangeStartChar>:#lo <CC_RangeTail #lo>
.

<CC_Ranges> (CC) ::=
  <CC_RangeChar>:#lo <CC_RangeTail #lo>
| {RegExpClassDone}:raw
.

<CC_RangeTail #lo> (CC) ::=
  CC_DASH ( {RegExpClassRange}:raw {{#lo}} <CC_RangeChar> <CC_Ranges> | {RegExpClassChar}:raw {{#lo}} {RegExpClassDone}:raw )
| {RegExpClassChar}:raw {{#lo}} <CC_NonDashRangeStartChar>:#lo2 <CC_RangeTail #lo2>
| {RegExpClassChar}:raw {{#lo}} {RegExpClassDone}:raw
.

<CC_FirstRangeStartChar> (CC) ::= (CC_RBRACKET$ | CC_DASH$ | CC_OTHER$) .
<CC_NonDashRangeStartChar> (CC) ::= (CC_NOT$ | CC_OTHER$) .
<CC_RangeChar> (CC) ::= (CC_NOT$ | CC_DASH$ | CC_OTHER$) .
